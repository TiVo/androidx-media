// Copyright (C) 2016 The Android Open Source Project
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
apply from: "$gradle.ext.androidxMediaSettingsDir/common_library_config.gradle"

android {
    defaultConfig {
        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    // Disable the E2E tests for now, they are notoriously unreliable on build servers,
    // ExoPlayer team is addressing this in later releases of AndroidX
    testOptions {
        unitTests.all { testTask ->
            if (System.getenv("GITHUB_ACTIONS") == "true") {
                testTask.exclude "**/e2etest/**"
            }
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
        }
    }

    sourceSets {
        androidTest.assets.srcDir '../test_data/src/test/assets'
        test.assets.srcDir '../test_data/src/test/assets/'
    }
}

// Version of AndroidX Media3 to pull from Google Maven (defaults to current release)
project.ext.baseAndroidxMediaVersion = project.ext.releaseVersion

dependencies {
    api 'androidx.media3:media3-common:' + baseAndroidxMediaVersion
    api 'androidx.media3:media3-container:' + baseAndroidxMediaVersion
    // TODO(b/203754886): Revisit which modules are exported as API dependencies.
    api 'androidx.media3:media3-datasource:' + baseAndroidxMediaVersion
    api 'androidx.media3:media3-decoder:' + baseAndroidxMediaVersion
    api project(modulePrefix + 'lib-extractor')
    api 'androidx.media3:media3-database:' + baseAndroidxMediaVersion
    implementation 'androidx.annotation:annotation:' + androidxAnnotationVersion
    implementation 'androidx.core:core:' + androidxCoreVersion
    compileOnly 'com.google.code.findbugs:jsr305:' + jsr305Version
    compileOnly 'org.checkerframework:checker-qual:' + checkerframeworkVersion
    compileOnly 'org.checkerframework:checker-compat-qual:' + checkerframeworkCompatVersion
    compileOnly 'org.jetbrains.kotlin:kotlin-annotations-jvm:' + kotlinAnnotationsVersion
    compileOnly 'com.google.errorprone:error_prone_annotations:' + errorProneVersion
    androidTestImplementation 'androidx.test:runner:' + androidxTestRunnerVersion
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker:' + dexmakerVersion
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito:' + dexmakerVersion
    androidTestImplementation 'androidx.media3:media3-test-utils:' + baseAndroidxMediaVersion
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:' + okhttpVersion
    testCompileOnly 'org.checkerframework:checker-compat-qual:' + checkerframeworkCompatVersion
    testImplementation 'com.squareup.okhttp3:mockwebserver:' + okhttpVersion
    testImplementation 'org.robolectric:robolectric:' + robolectricVersion
    testImplementation 'androidx.media3:media3-test-utils:' + baseAndroidxMediaVersion
    testImplementation 'androidx.media3:media3-test-utils-robolectric:' + baseAndroidxMediaVersion
}

ext {
    javadocTitle = 'Core module'
}
apply from: '../../javadoc_library.gradle'

ext {
    releaseArtifactId = 'media3-exoplayer'
    releaseName = 'Media3 ExoPlayer module'
}
apply from: '../../publish-tivo.gradle'
