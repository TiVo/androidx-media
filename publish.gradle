// Copyright (C) 2017 The Android Open Source Project
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

apply plugin: 'maven-publish'


if (project.ext.has("tivoExoplayerPublishEnabled")
        && project.ext.tivoExoplayerPublishEnabled) {
    ext {
        releaseGroupId = 'com.tivo.android.exoplayer'
    }

    if (project.ext.releaseVersion.endsWith("-dev") && System.getenv('BUILD_NUMBER') != null) {
        project.ext.releaseVersion = project.ext.releaseVersion + "-" + System.getenv('BUILD_NUMBER')
    }
} else {
    ext {
        releaseGroupId = 'com.google.android.exoplayer'
    }
}

afterEvaluate {
    publishing {
        repositories {
            if (project.ext.has("tivoExoplayerPublishEnabled")
                    && project.ext.tivoExoplayerPublishEnabled) {
                maven {
                    url "https://repo-vip.tivo.com:8443/artifactory/libs-release-local"
                    credentials {
                        username REPO_USER_NAME
                        password REPO_PASSWORD
                    }
                }
            } else {
                maven {
                    url = findProperty('mavenRepo') ?: "${buildDir}/repo"
                }
            }
        }
        publications {
            release(MavenPublication) {
                if (components.any( {it.name.contains("apk")})) {
                    from components.debug_apk
                } else {
                    from components.release
                }
                artifact androidSourcesJar
                groupId = releaseGroupId
                artifactId = releaseArtifact
                version releaseVersion
                pom {
                    name = releaseArtifact
                    description = releaseDescription
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            name = 'The Android Open Source Project'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/google/ExoPlayer.git'
                        url = 'https://github.com/google/ExoPlayer'
                    }
                }
            }
        }
    }
}
tasks.withType(PublishToMavenRepository) { it.dependsOn lint, test }
tasks.withType(PublishToMavenRepository) {
    doFirst {
        println("Publishing ${publication.groupId}:${publication.artifactId}:${publication.version} to ${repository.url}")
    }
}
tasks.withType(PublishToMavenLocal) {
    doFirst {
        println("Publishing ${publication.groupId}:${publication.artifactId}:${publication.version} to maven local repo")
    }
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}
